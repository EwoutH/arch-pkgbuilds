# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=dav1d
pkgver=0.2.0
pkgrel=1
pkgdesc='AV1 cross-platform decoder focused on speed and correctness'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('meson' 'ninja' 'nasm' 'doxygen')
provides=('libdav1d.so')
source=(${pkgname}-${pkgver}.tar.bz2::https://code.videolan.org/videolan/dav1d/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('378060ed6762f9f6baf7addfa7382cb0873bbc606f0389eee4ed7537dcfc851f5b159a34af0f15d9f8d345a9b2a950f38078a419144b0458aa3464e54ea5d490')

build() {
  cd ${pkgbase}-${pkgver}
  arch-meson build
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}/build
  meson test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
